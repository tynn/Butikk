//  Copyright 2019 Christian Schmitz
//  SPDX-License-Identifier: Apache-2.0

buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61' apply false
    id 'org.jetbrains.dokka' version '0.10.0'
    id 'xyz.tynn.idea.fix' version '0.1.0'
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    plugins.withId('org.jetbrains.dokka') {
        dokka {
            configuration {
                externalDocumentationLink {
                    url = new URL('https://developer.android.com/reference/kotlin/')
                    packageListUrl = new URL(url, '../androidx/package-list')
                }
                externalDocumentationLink {
                    url = new URL('https://kotlin.github.io/kotlinx.coroutines/kotlinx-coroutines-core/')
                    packageListUrl = new URL(url, 'package-list')
                }
            }
        }
    }
}

subprojects {
    group = 'xyz.tynn.butikk'

    tasks.withType(Test) {
        testLogging {
            events "failed"
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
        }
    }

    plugins.withId('kotlin') {
        project.apply plugin: 'maven-publish'
        project.apply plugin: 'jacoco'

        java.withSourcesJar()
        components.java.addVariantsFromConfiguration(configurations.kdocElements) {}

        publishing {
            publications {
                kotlinJvm(MavenPublication) {
                    from components.java
                }
            }
        }

        dependencies {
            api Deps.kotlin.stdlib
            testImplementation Deps.kotlin.test.junit
        }
    }

    plugins.withId('com.android.library') {
        project.apply plugin: 'xyz.tynn.android.adhoc'
        project.apply plugin: 'maven-publish'

        components.android.addVariantsFromConfiguration(configurations.kdocElements) {}

        publishing {
            publications {
                androidJvm(MavenPublication) {
                    from components.android
                }
            }
        }
    }

    plugins.withType(com.android.build.gradle.BasePlugin) {
        project.apply plugin: 'kotlin-android'

        android {
            compileSdkVersion 29
            buildToolsVersion '29.0.2'

            defaultConfig {
                minSdkVersion 14
                targetSdkVersion 29
                testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
            }

            compileOptions {
                sourceCompatibility '1.8'
                targetCompatibility '1.8'
            }

            kotlinOptions {
                jvmTarget = '1.8'
            }
        }

        dependencies {
            implementation Deps.kotlin.stdlib
        }
    }

    plugins.withType(JacocoPlugin) {
        jacoco.toolVersion = '0.8.4'

        tasks.withType(JacocoReport) {
            check.dependsOn it
            reports.xml.enabled true
        }
    }

    plugins.withType(MavenPublishPlugin) {
        project.apply plugin: 'org.jetbrains.dokka'
        project.apply from: "$rootDir/publish.gradle"
    }

    plugins.withId('org.jetbrains.dokka') {
        project.apply plugin: 'xyz.tynn.kdoc.elements'
        rootProject.dokka.subProjects += project.name

        dokka {
            outputFormat = 'html'
            outputDirectory = "$buildDir/kdoc"
        }

        tasks.named('kdocJar') {
            dependsOn dokka
            from fileTree(dokka.outputDirectory)
        }
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/docs"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
